buildscript {
    repositories {
        maven {
            name = "gradle"
            url = "https://plugins.gradle.org/m2/"
        }
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'net.ltgt.apt' version '0.10'
    id 'eclipse'
}

apply plugin: "java"
apply plugin: 'net.minecraftforge.gradle.forge'


// ==========================================
//            Load Secrets
// ==========================================
apply from: "https://raw.githubusercontent.com/ReadOnly-Mods/Misc/master/Gradle/secret.gradle"
loadSecrets()
// ==========================================


version = "1.0.0"
group = "net.romvoid95.mcdeath"
archivesBaseName = "ReaperFlyGuyDeath"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

repositories {

}

dependencies {

}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "stable_39"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    onlyIf {
        project.hasProperty('keyStore')
    }

    inputFile = jar.archivePath
    outputFile = jar.archivePath

    keyStore = findProperty('keyStore')
    alias = findProperty('keyStoreAlias')
    storePass = findProperty('keyStorePass')
    keyPass = findProperty('keyStoreKeyPass')
}

curseforge {
    if (secret.containsKey('curseapi')) {
        apiKey = secret.curseapi
        project {
            id = "406123"
            changelog = file("CHANGELOG.md")
            changelogType = "markdown"
            releaseType = "release"
            addGameVersion mc_version

            mainArtifact(jar) {
                displayName = "GalacticTweaks-" + mc_version + "-" + version
            }
        }
    }
}

build.dependsOn signJar
tasks.curseforge.dependsOn(build)